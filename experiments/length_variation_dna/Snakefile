import glob
import re

configfile: "../common_params.json"
ITERATIONS=config['n_iterations']
THINNING=config['thinning']
DATA_SETS = range(config['n_data_sets'])
SEQ_LENGTHS=config['seq_lengths']

gamma_k = 2
gamma_theta = 0.5

rule all:
   input:
      "figures/rf_summary.pdf",
      "figures/rfm_summary_with_title.pdf"

rule gen_guest_tree:
   params: prefix="sim_data/{len_n_num}"
   output: "sim_data/{len_n_num}.pruned.tree", "sim_data/{len_n_num}.pruned.leafmap", "sim_data/{len_n_num}.pruned.info"
   shell: JPRIME + " GuestTreeGen -minper 1 -min 10 -max 20 " + S + " 1.0 1.0 0.0 {params.prefix}"

rule relax_branches1:
     input: "sim_data/{prefix}.pruned.tree"
     output: "sim_data/{prefix}.relaxed.tree"
     shell: JPRIME + " BranchRelaxer {input} IIDGamma {gamma_k} {gamma_theta} -o {output}"

rule relax_branches2:
     input: "sim_data/{prefix}.pruned.tree"
     output: "sim_data/{prefix}.relaxed_aux.tree"
     shell: JPRIME + " BranchRelaxer {input} IIDGamma {gamma_k} {gamma_theta} -o {output} -x"
     # Include auxilliary information (JPrIME format) in the tree

rule seq_gen_phylip:
     input: "sim_data/n{num}.relaxed.tree"
     output: temp("sim_data/L{length}_n{num}.phylip")
     params: l="{length}"
     shell: "seq-gen -mHKY -l {params.l} < {input} > {output}"

rule phylip2fasta:
     input: "sim_data/{prefix}.phylip"
     output: "sim_data/{prefix}.fa"
     shell: "phylip2fasta {input} > {output}"


rule run_jprime:
   input:
      S,
      "sim_data/L{length}_n{num}.fa",
      "sim_data/n{num}.pruned.leafmap"
   output:
      "traces/L{length}_n{num}.trace"
   shell:
      JPRIME + " Delirious -sm JC69 --iterations {ITERATIONS} --thinning {THINNING} --outfile {output} {input}"

rule run_vmcmc:
     input: 'traces/{prefix}.trace'
     output: 'traces/{prefix}_map.json'
     shell: VMCMC + ' -b -1 -m {input} > {output}'

rule get_map_tree:
     input:	'traces/{prefix}_map.json'
     output:	'traces/{prefix}_map.tree'
     shell: GETMAPTREE + ' {input} > {output}'

rule get_posterior:
     input:	'traces/{prefix}.trace'
     output:	'traces/{prefix}_posterior.json'
     shell: VMCMC + ' -b -1 -p {input} > {output}'

rule compare_tree:
   input:
      'sim_data/n{num}.relaxed.tree',
      'traces/L{length}_n{num}_map.tree'
   output:
      'assessment/L{length}_n{num}_rf.txt'
   shell:
      '../../bin/rf {input} > {output}'

rule compare_posterior:
   input:
      'sim_data/n{num}.relaxed.tree',
      'traces/L{length}_n{num}_posterior.json'
   output:
      'assessment/L{length}_n{num}_rfm.txt'
   shell:
      '../../bin/mean_rf {input} > {output}'


rule tree_sizes:
   input: ["sim_data/n" + str(N) + ".pruned.info" for N in DATA_SETS]
   output: temp("tree_sizes.tab")
   shell: "grep leaves {input} > {output}"

rule tree_size_bar_chart:
   input: "tree_sizes.tab"
   output: "figures/tree_sizes.pdf"
   shell: "cat {input} | cut -f 3 -d:  | sort -n | uniq -c | " + BAR_CHART + "  -title Tree_sizes figures/tree_sizes"

rule rf_summary_per_length:
   input: ["assessment/L{length}_n" + str(N) + "_rf.txt" for N in DATA_SETS]
   output: "assessment/L{length}_rf_summary.txt"
   shell: "cat {input} > {output}"

rule mean_rf_summary_per_length:
   input: ["assessment/L{length}_n" + str(N) + "_rfm.txt" for N in DATA_SETS]
   output: "assessment/L{length}_rfm_summary.txt"
   shell: "cat {input} > {output}"


rule plot_rf_per_length:
   input: ["assessment/L" + str(L) + "_rf_summary.txt" for L in SEQ_LENGTHS]
   output:
      "figures/rf_summary.pdf",
      "figures/rf_summary_with_title.pdf"
   shell: BOXPLOTS + " {output[0]} {input} && " + BOXPLOTS + " -title 'MAP Robinson-Foulds' {output[1]} {input}"

rule plot_mean_rf_per_length:
   input: ["assessment/L" + str(L) + "_rfm_summary.txt" for L in SEQ_LENGTHS]
   output:
      "figures/rfm_summary.pdf",
      "figures/rfm_summary_with_title.pdf"
   shell: BOXPLOTS + " {output[0]} {input} && " + BOXPLOTS + " -title 'Mean posterior Robinson-Foulds (DNA)' {output[1]} {input}"
     